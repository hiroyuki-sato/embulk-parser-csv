plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "checkstyle"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.11.0-SNAPSHOT"
description = "Parses CSV files read by other file input plugins."

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }

    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-spi:0.11"
    compileOnly "org.slf4j:slf4j-api:2.0.7"

    implementation "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-core:2.6.7"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.6.7.5"
    implementation "org.embulk:embulk-util-csv:0.2.3"
    implementation "org.embulk:embulk-util-config:0.3.3"
    implementation "org.embulk:embulk-util-file:0.1.4"
    implementation "org.embulk:embulk-util-json:0.2.1"
    implementation "org.embulk:embulk-util-text:0.1.2"
    implementation "org.embulk:embulk-util-timestamp:0.2.2"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.embulk:embulk-spi:0.10.44"
    testImplementation "org.embulk:embulk-core:0.10.44"
    testImplementation "org.embulk:embulk-deps:0.10.44"
    testImplementation "org.embulk:embulk-junit4:0.10.44"
    testImplementation "com.google.guava:guava:18.0"

    testImplementation "org.embulk:embulk-formatter-csv:0.10.42"
    testImplementation "org.embulk:embulk-input-file:0.10.42"
    testImplementation "org.embulk:embulk-output-file:0.10.42"
    testImplementation "org.embulk:embulk-parser-csv:0.10.42"
    testImplementation "org.embulk:embulk-guess-bzip2:0.10.42"
    testImplementation "org.embulk:embulk-guess-csv:0.10.42"
    testImplementation "org.embulk:embulk-guess-gzip:0.10.42"
    testImplementation "org.embulk:embulk-guess-json:0.10.42"
}

embulkPlugin {
    mainClass = "org.embulk.parser.csv.CsvParserPlugin"
    category = "parser"
    type = "csv"
}

jar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

sourcesJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

javadocJar {
    metaInf {
        from rootProject.file("LICENSE")
    }
}

publishing {
    publications {
         maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java
             // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
             // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = "repo"
                    }
                }

                developers {
                    developer {
                        name = "Muga Nishizawa"
                        email = "muga.nishizawa@gmail.com"
                    }
                    developer {
                        name = "Sadayuki Furuhashi"
                        email = "frsyuki@gmail.com"
                    }
                    developer {
                        name = "Shinichi Ishimura"
                        email = "shiketaudonko41@gmail.com"
                    }
                    developer {
                        name = "Naotoshi Seo"
                        email = "sonots@gmail.com"
                    }
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@acm.org"
                    }
                    developer {
                        name = "Satoshi Akama"
                        email = "satoshiakama@gmail.com"
                    }
                    developer {
                        name = "Roman Shtykh"
                        email = "rshtykh@yahoo.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-parser-csv.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-parser-csv.git"
                    url = "https://github.com/embulk/embulk-parser-csv"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    if (project.hasProperty("signingKey") && project.hasProperty("signingPassword")) {
        logger.lifecycle("Signing with an in-memory key.")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.maven
}

test {
    maxHeapSize = "1536m"
    forkEvery = 1  // test processes are forked by each test class (default is 0)
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        exceptionFormat = org.gradle.api.tasks.testing.logging.TestExceptionFormat.FULL
        showCauses = true
        showExceptions = true
        showStackTraces = true
        showStandardStreams = true
        outputs.upToDateWhen { false }
    }
}

tasks.withType(Checkstyle) {
    reports {
        // Not to skip up-to-date checkstyles.
        outputs.upToDateWhen { false }
    }
}

checkstyle {
    toolVersion "8.7"
    configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    configProperties = [
        "checkstyle.config.path": file("${projectDir}/config/checkstyle")
    ]
    ignoreFailures = false
    maxWarnings = 0  // https://github.com/gradle/gradle/issues/881
}
